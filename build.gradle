/*
 * Copyright (c) 2016, nwillc@gmail.com
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

plugins {
    id "groovy"
    id "com.gradle.plugin-publish" version "0.9.6"
    id "com.github.nwillc.vplugin" version "2.1.1"
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.apache.maven:maven-artifact:3.6.0"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.3.1",
            "org.assertj:assertj-core:3.11.1"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.3.1"
}

group = "com.github.nwillc"
version = "2.2.0"
targetCompatibility = "1.8"
sourceCompatibility = "1.8"

test {
    useJUnitPlatform()

    beforeTest { descriptor ->
        logger.lifecycle("\tRunning $descriptor.className.$descriptor.name")
    }

    afterSuite { descriptor, result ->
        if (descriptor.parent == null) {
            logger.lifecycle("\tTests run: $result.testCount, Failures: $result.failedTestCount, Skipped: $result.skippedTestCount")
        }
    }

    testLogging {
        exceptionFormat "full"
        showStandardStreams true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = "groovydoc"
    from groovydoc.destinationDir
}

groovydocJar.dependsOn "groovydoc"

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}


pluginBundle {
    website = "https://github.com/nwillc/vplugin"
    vcsUrl = "https://github.com/nwillc/vplugin"
    description = """Gradle plugin to report newer versions of dependencies. Traverses your plugin,
compile and runtime dependencies. For each dependency, all of your declared repositories are
checked, and the highest version is found. A text report is generated showing the dependencies, 
their current version, and higher ones if available.
"""
    tags = ["dependencies", "versions"]

    plugins {
        versionsPlugin {
            id = "com.github.nwillc.vplugin"
            displayName = "Gradle versions plugin"
        }
    }
}
